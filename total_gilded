#!/usr/bin/perl

# Given a single story, show how many times that story has been gilded, across all comments in that
# story.
#
# Example call:
#       ./total_gilded http://www.reddit.com/r/blog/comments/24seva | xclip -selection clipboard

    use strict;
    use warnings;

    use LWP::Simple;
    use CGI;
    use Data::Dumper;

    # for DEVELOPMENT PURPOSES ONLY
    #@ARGV = ('http://www.reddit.com/r/blog/comments/24seva/were_fighting_for_marriage_equality_in_utah_and/');

binmode(STDOUT, ":utf8");

my $url = shift
    or die "Specify the URL of a specific story.\n";

$url =~ m#http://(?:www\.)?reddit\.com/r/([^/]+)/comments/([^/]+)#i
    or die "Story URL must be of the form http://www.reddit.com/r/subreddit/comments/6789ab\n";

my $subreddit = $1;
my $story_id  = $2;

my $total = 0;
my @parsed_comments;

my @gilded_report_urls = ("http://www.reddit.com/r/$subreddit/gilded");
while (@gilded_report_urls) {
    my $gilded_report_url = shift @gilded_report_urls;
    my $gilded_html = get $gilded_report_url;

    print STDERR ">> $gilded_report_url\n";

    my $total_this_time = 0;
    while ($gilded_html =~ m#(<div [^>]*\bclass=["']?[^>"]*gilded comment.*?</form>.*?>permalink<.*?</div>)#sig) {
        my $comment = $1;
        #print $comment; exit;

        my %parsed;

        ($parsed{gildings}) = ($comment =~ m# class="gilded-icon"[^>]*>x(\d+)#si);
        $parsed{gildings} = 1 unless defined($parsed{gildings});
        ($parsed{text})     = ($comment =~ m#<form[^>]*>(.*?)</form>#si);
        ($parsed{id})       = ($comment =~ m#<a name="([^"]*)#si);
        ($parsed{url})      = ($comment =~ m#<a href="([^"]*)[^>]*>permalink<#si);

        next if ($parsed{url} !~ $story_id);        # We're scanning ALL gildings in this subreddit, so ignore gildings that aren't associated with this specific story.

        $total += $parsed{gildings};
        $total_this_time += $parsed{gildings};
        #print Dumper \%parsed;

        push @parsed_comments, \%parsed;
    }
    last if ($total_this_time == 0);

    if ($gilded_html =~ m#(http://[^'"]*after=t1_[^'"]*)#si) {
        push @gilded_report_urls, CGI::unescapeHTML($1);
    }
}

## sort by number of gildings, followed by most recent first
@parsed_comments = sort { $b->{gildings} <=> $a->{gildings} ||
                $b->{id} cmp $a->{id}} @parsed_comments;

## output the data
print "There have been $total total gildings so far:\n\n";
foreach my $comment (@parsed_comments) {
    print "* ";
    if ($comment->{gildings} > 1) {
        print "x$comment->{gildings} ";
    }
    my $text = html2text($comment->{text});
    if (length($text) > 80) {       # truncate long comments
        $text = substr($text, 0, 80);
        $text =~ s/\W\w+$//s;       # truncate at a word boundary
        $text =~ s/[\.\s]+$//s;
        $text .= " " . chr(8230);   # &hellip;
    }
    print "[$text]($comment->{url}?context=3)\n";
}
print "\n[(source code)](https://github.com/DeeNewcum/reddit/blob/master/total_gilded)\n";


sub html2text {
    local $_ = shift @_;
    # remove HTML tags
    s/<[^>]*>//sg;
    # unescape HTML entities
    $_ = CGI::unescapeHTML($_);
    $_ =~ s/[\n\r]//sg;
    return $_;
}

