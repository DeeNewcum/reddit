#!/usr/bin/perl

# Pass in a URL to a story/comment/user/subreddit, and it displays the exact date/time that the
# story/comment/etc was created.

    use strict;
    use warnings;

    use LWP::Simple;
    use JSON::XS;
    use Data::Dumper;

    # see http://www.reddit.com/dev/api#fullnames
    my %type_prefixes = (
        t1 => 'comment',
        t2 => 'user',       # officially called an "account"
        t3 => 'story',      # officially called a "link", I prefer "story", since it's a link + comments
        t4 => 'message',
        t5 => 'subreddit',
        t6 => 'award',
        t8 => 'promo campaign',
    );

    # for DEVELOPMENT PURPOSES only -- should be commented out normally
    #@ARGV = 'http://www.reddit.com/r/blog/comments/1osafg/thanks_for_the_gold/'  unless @ARGV;
    #@ARGV = 'http://www.reddit.com/r/meta_feminism' unless @ARGV;

my $json_url = shift
    or die "Specify the URL of a story/comment/user/subreddit\n";

# calculate $request_type, since I don't know of a way to get the server to tell us this
my ($request_type, $request_id) = (undef, '');
if ($json_url =~ m#reddit\.com/r/[^/]*/?$#i) {
    $request_type = 't5';       # subreddit
    # If this is a subreddit, then get the subreddit's metadata.  If we didn't append "/about", then
    # we would get a list of posts, which isn't what we want.
    $json_url =~ s#/?$#/about/.json#;
} elsif ($json_url =~ m#reddit\.com/r/[^/]*/comments/([^/]*)/[^/]*/?$#i) {
    $request_type = 't3';       # link (story)
    $request_id = $1;
} elsif ($json_url =~ m#reddit\.com/r/[^/]*/comments/[^/]*/[^/]*/([^/]*)/?$#i) {
    $request_type = 't1';       # comment
    $request_id = $1;
} elsif ($json_url =~ m#reddit\.com/user/[^/]*/?$#i) {
    $request_type = 't2';       # user
    $json_url =~ s#/?$#/about.json#;
}

# append ".json" if it isn't already there
$json_url =~ s#(/(\.json)?)?$#/.json#i
        unless ($request_type eq 't2');
print "Fetching $json_url\n\n";


my $json = get $json_url
    or die "Unable to fetch that URL\n";

$json = decode_json $json;
        # die Dumper $json;

    # To debug this info, run something like this:
    #       curl http://www.reddit.com/r/blog/comments/1osafg/thanks_for_the_gold/.json | pretty -j -
    # That *should* be the exact same data, though it's possible it isn't.
    # That uses this utility:  https://github.com/DeeNewcum/dotfiles/blob/master/bin/pretty

if (($request_type || '') eq 't1') {        # comment
    $json = $json->[1];
} else {
    $json = $json->[0]      if (ref($json) eq 'ARRAY');
}

if ($json->{kind} eq 'Listing') {
    $json = $json->{data}{children}[0];
}

if (!defined($request_type)) {
    $request_type = $json->{kind};
}
my $created      = $json->{data}{created_utc};
my $last_updated = $json->{data}{edited};
            #die Dumper [$kind, $created, $last_updated];

print "This $type_prefixes{$request_type} was created on:\n\t",
        scalar(gmtime($created)), "\n";

if (defined($last_updated) && $last_updated > 0 && $last_updated != $created) {
    print "and last updated on:\n\t",
        scalar(gmtime($last_updated)), "\n";
}

